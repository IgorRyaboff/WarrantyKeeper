
#Область СлужебныеПроцедурыИФункции

Процедура ВыполнитьДействияПриЗапуске() Экспорт
	ОчиститьУстаревшиеДанные();
	ПереместитьИстекшиеЭлементыВКорзину();
КонецПроцедуры

Процедура ПереместитьИстекшиеЭлементыВКорзину()
	Если Не Константы.АвтоматическиПеремещатьИстекшиеТалоныВКорзину.Получить() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГарантийныеТалоны.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ГарантийныеТалоны КАК ГарантийныеТалоны
		|ГДЕ
		|	НЕ ГарантийныеТалоны.ПометкаУдаления
		|	И ГарантийныеТалоны.ДатаОкончанияГарантии < &Дата";
	
	Запрос.УстановитьПараметр("Дата", ТекущаяДатаСеанса());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Элемент = Выборка.Ссылка.ПолучитьОбъект();
		Элемент.УстановитьПометкуУдаления(Истина);
	КонецЦикла;
КонецПроцедуры

Процедура ОчиститьУстаревшиеДанные()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГарантийныеТалоны.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ГарантийныеТалоны КАК ГарантийныеТалоны
		|ГДЕ
		|	ГарантийныеТалоны.ПометкаУдаления
		|	И ГарантийныеТалоны.ДатаПометкиУдаления < &Дата";
	
	СекундВСутках = 86400;
	СрокХраненияВСутках = Константы.СрокХраненияТалоновВКорзине.Получить();
	Запрос.УстановитьПараметр("Дата", ТекущаяДатаСеанса() - (СекундВСутках * СрокХраненияВСутках));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Элемент = Выборка.Ссылка.ПолучитьОбъект();
		Элемент.Удалить();
	КонецЦикла;
КонецПроцедуры

Процедура ЗаписатьДанныеВРезервнуюКопию(Каталог) Экспорт
	КаталогТалонов = Каталог + "warranty-cards" + ПолучитьРазделительПути();
	КаталогПрикрепленныхФото = Каталог + "warranty-cards-pics" + ПолучитьРазделительПути();
	
	СоздатьКаталог(КаталогТалонов);
	СоздатьКаталог(КаталогПрикрепленныхФото);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГарантийныеТалоны.Ссылка КАК Ссылка,
		|	ГарантийныеТалоны.ПометкаУдаления КАК ПометкаУдаления,
		|	ГарантийныеТалоны.Код КАК Код,
		|	ГарантийныеТалоны.Наименование КАК Наименование,
		|	ГарантийныеТалоны.ДатаПокупки КАК ДатаПокупки,
		|	ГарантийныеТалоны.СрокГарантии КАК СрокГарантии,
		|	ГарантийныеТалоны.ДатаОкончанияГарантии КАК ДатаОкончанияГарантии,
		|	ГарантийныеТалоны.Комментарий КАК Комментарий,
		|	ГарантийныеТалоны.ДатаПометкиУдаления КАК ДатаПометкиУдаления
		|ИЗ
		|	Справочник.ГарантийныеТалоны КАК ГарантийныеТалоны
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГарантийныеТалоныПрикрепленныеФото.Ссылка КАК Ссылка,
		|	ГарантийныеТалоныПрикрепленныеФото.НомерСтроки КАК НомерСтроки,
		|	ГарантийныеТалоныПрикрепленныеФото.ХранилищеКартинки КАК ХранилищеКартинки
		|ИЗ
		|	Справочник.ГарантийныеТалоны.ПрикрепленныеФото КАК ГарантийныеТалоныПрикрепленныеФото
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаЭлементы = РезультатЗапроса[0].Выбрать();
	ВыборкаФото = РезультатЗапроса[1].Выбрать();
	
	Пока ВыборкаЭлементы.Следующий() Цикл
		БазовоеИмяФайла = Строка(ВыборкаЭлементы.Ссылка.УникальныйИдентификатор());
		Данные = ВыборкаЭлементыВСтруктуру(ВыборкаЭлементы);
		
		ЗаписьТекста = Новый ЗаписьТекста(КаталогТалонов + БазовоеИмяФайла + ".json");
		ЗаписьТекста.Записать(ЗаписатьЗначениеJSON(Данные));
		ЗаписьТекста.Закрыть();
	КонецЦикла;
	
	Пока ВыборкаФото.Следующий() Цикл
		ИдентификаторТалона = Строка(ВыборкаЭлементы.Ссылка.УникальныйИдентификатор());
		ИмяФайла = СтрШаблон("%1-%2", ИдентификаторТалона, Формат(ВыборкаФото.НомерСтроки, "ЧГ="));
		
		ДвоичныеДанные = ВыборкаФото.ХранилищеКартинки.Получить().ПолучитьДвоичныеДанные();
		ДвоичныеДанные.Записать(КаталогПрикрепленныхФото + ИмяФайла);
	КонецЦикла;
КонецПроцедуры

Функция ВыборкаЭлементыВСтруктуру(ВыборкаЭлементы)
	Данные = Новый Структура;
	
	Данные.Вставить("ПометкаУдаления", ВыборкаЭлементы.ПометкаУдаления);
	Данные.Вставить("Код", ВыборкаЭлементы.Код);
	Данные.Вставить("Наименование", ВыборкаЭлементы.Наименование);
	Данные.Вставить("ДатаПокупки", Строка(ВыборкаЭлементы.ДатаПокупки));
	Данные.Вставить("СрокГарантии", ВыборкаЭлементы.СрокГарантии);
	Данные.Вставить("ДатаОкончанияГарантии", Строка(ВыборкаЭлементы.ДатаОкончанияГарантии));
	Данные.Вставить("Комментарий", ВыборкаЭлементы.Комментарий);
	Данные.Вставить("ДатаПометкиУдаления", Строка(ВыборкаЭлементы.ДатаПометкиУдаления));
	
	Возврат Данные;
КонецФункции

#КонецОбласти
