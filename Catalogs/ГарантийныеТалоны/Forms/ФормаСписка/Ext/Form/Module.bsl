
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьУсловноеОформление();
	
	Сортировка = ХранилищеОбщихНастроек.Загрузить("ГарантийныеТалоны.ФормаСписка", "Сортировка");
	Если Не ЗначениеЗаполнено(Сортировка) Тогда
		Сортировка = "Код Возр";
	КонецЕсли;
	УстановитьПорядокСписка();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СортировкаПриИзменении(Элемент)
	УстановитьПорядокСписка();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ПереместитьВКорзину(Команда)
	Ссылка = Элементы.Список.ТекущаяСтрока;
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru='Талон будет перенесен в Корзину и удален через 7 дней. Продолжить?'");
	Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ПометитьЭлементНаУдаление(Ссылка);
	ОповеститьОбИзменении(Ссылка);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ПометитьЭлементНаУдаление(Знач Ссылка)
	Объект = Ссылка.ПолучитьОбъект();
	Объект.УстановитьПометкуУдаления(Истина);
	Объект.Записать();
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ШрифтТалонСИстекшимСроком);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТалонСИстекшимСроком);
	
	ЭлементОтбора = ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.СрокГарантииИстек");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Истина;
	
	ПолеУО = ЭлементУО.Поля.Элементы.Добавить();
	ПолеУО.Поле = Новый ПолеКомпоновкиДанных(Элементы.Список.Имя);
КонецПроцедуры

&НаСервере
Процедура УстановитьПорядокСписка()
	ИдентификаторНастройки = Список.Порядок.ИдентификаторПользовательскойНастройки;
	Порядок = Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ИдентификаторНастройки);
	Порядок.Элементы.Очистить();
	
	СортировкаЧасти = СтрРазделить(Сортировка, " ");
	ПолеСортировки = СортировкаЧасти[0];
	ТипСортировки = СортировкаЧасти[1];
	
	ЭлементПорядка = Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных(ПолеСортировки);
	ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных[ТипСортировки];
	
	ХранилищеОбщихНастроек.Сохранить("ГарантийныеТалоны.ФормаСписка", "Сортировка", Сортировка);
КонецПроцедуры

#КонецОбласти
