
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		КопируемыйОбъект = Параметры.ЗначениеКопирования.ПолучитьОбъект();
		ПрочитатьКартинкиИзОбъекта(КопируемыйОбъект);
	КонецЕсли;
	
	ОбновитьГалереюФото();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ПрочитатьКартинкиИзОбъекта(ТекущийОбъект);
	ОбновитьГалереюФото();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ПеренестиКартинкиВОбъект(ТекущийОбъект);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ПрочитатьКартинкиИзОбъекта(ТекущийОбъект);
	ОбновитьГалереюФото();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПокупкиПриИзменении(Элемент)
	ПересчитатьДатуОкончанияГарантии();
КонецПроцедуры

&НаКлиенте
Процедура СрокГарантииПриИзменении(Элемент)
	ПересчитатьДатуОкончанияГарантии();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ПрикрепитьФото(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		Если Не СредстваМультимедиа.ПоддерживаетсяФотоснимок(ТипКамерыУстройства.Задняя) Тогда
			ПоказатьПредупреждение(, НСтр("ru='Устройство не поддерживает съемку фото'"));
			Возврат;
		КонецЕсли;
		
		ДанныеМультимедиа = СредстваМультимедиа.СделатьФотоснимок(ТипКамерыУстройства.Задняя);
		Если ДанныеМультимедиа = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ДвоичныеДанные = ДанныеМультимедиа.ПолучитьДвоичныеДанные();
	#Иначе
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		Диалог.МножественныйВыбор = Ложь;
		
		ВыбранныеФайлы = Ждать Диалог.ВыбратьАсинх();
		Если ВыбранныеФайлы = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ДвоичныеДанные = Новый ДвоичныеДанные(ВыбранныеФайлы[0]);
	#КонецЕсли
	
	Картинка = Новый Картинка(ДвоичныеДанные);
		
	СтрФото = Объект.ПрикрепленныеФото.Добавить();
	СтрФото.Картинка = Картинка;
	СтрФото.АдресКартинки = ПоместитьВоВременноеХранилище(Картинка, УникальныйИдентификатор);
	Модифицированность = Истина;
	
	ОбновитьГалереюФото();
	
	Индекс = Объект.ПрикрепленныеФото.Индекс(СтрФото);
	Элементы.ГруппаГалереяФото.ТекущаяСтраница = Элементы.Найти("ГруппаПрикрепленноеФото" + Формат(Индекс, "ЧН=0; ЧГ="));
КонецПроцедуры

&НаКлиенте
Процедура УдалитьТекущееФото(Команда)
	Группа = Элементы.ГруппаГалереяФото.ТекущаяСтраница;
	Если Группа = Неопределено Тогда
		Группа = Элементы.ГруппаГалереяФото.ПодчиненныеЭлементы.Получить(0);
	КонецЕсли;
	
	Индекс = Число(СтрЗаменить(Группа.Имя, "ГруппаПрикрепленноеФото", ""));
	
	Объект.ПрикрепленныеФото.Удалить(Индекс);
	Модифицированность = Истина;
	
	ОбновитьГалереюФото();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьКартинкиИзОбъекта(ИсходныйОбъект)
	Для Каждого СтрИзФормы Из Объект.ПрикрепленныеФото Цикл
		СтрОбъекта = ИсходныйОбъект.ПрикрепленныеФото.Найти(СтрИзФормы.НомерСтроки, "НомерСтроки");
		СтрИзФормы.Картинка = СтрОбъекта.ХранилищеКартинки.Получить();
		СтрИзФормы.АдресКартинки = ПоместитьВоВременноеХранилище(СтрИзФормы.Картинка, УникальныйИдентификатор);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПеренестиКартинкиВОбъект(СохраняемыйОбъект)
	Для Каждого СтрИзФормы Из Объект.ПрикрепленныеФото Цикл
		СтрОбъекта = СохраняемыйОбъект.ПрикрепленныеФото.Найти(СтрИзФормы.НомерСтроки, "НомерСтроки");
		СтрОбъекта.ХранилищеКартинки = Новый ХранилищеЗначения(СтрИзФормы.Картинка);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьДатуОкончанияГарантии()
	Если ЗначениеЗаполнено(Объект.ДатаПокупки) И ЗначениеЗаполнено(Объект.СрокГарантии) Тогда
		Объект.ДатаОкончанияГарантии = ДобавитьМесяц(Объект.ДатаПокупки, Объект.СрокГарантии);
	Иначе
		Объект.ДатаОкончанияГарантии = Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьГалереюФото()
	УдаляемыеЭлементы = Новый Массив;
	Для Каждого Группа Из Элементы.ГруппаГалереяФото.ПодчиненныеЭлементы Цикл
		Индекс = Число(СтрЗаменить(Группа.Имя, "ГруппаПрикрепленноеФото", ""));
		Если Индекс > (Объект.ПрикрепленныеФото.Количество() - 1) Тогда
			УдаляемыеЭлементы.Добавить(Группа);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Элемент Из УдаляемыеЭлементы Цикл
		Элементы.Удалить(Элемент);
	КонецЦикла;
	
	Для Индекс = 0 По (Объект.ПрикрепленныеФото.Количество() - 1) Цикл
		СтрФото = Объект.ПрикрепленныеФото[Индекс];
		
		ИмяСтраницы = СтрШаблон("ГруппаПрикрепленноеФото%1", Формат(Индекс, "ЧН=0; ЧГ="));
		Страница = Элементы.Найти(ИмяСтраницы);
		Если Страница = Неопределено Тогда
			Страница = НоваяСтраницаПрикрепленноеФото(Индекс, ИмяСтраницы);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция НоваяСтраницаПрикрепленноеФото(Индекс, ИмяСтраницы)
	Страница = Элементы.Добавить(ИмяСтраницы, Тип("ГруппаФормы"), Элементы.ГруппаГалереяФото);
	Страница.Вид = ВидГруппыФормы.Страница;
	
	Картинка = Элементы.Добавить(ИмяСтраницы + "Картинка", Тип("ПолеФормы"), Страница);
	Картинка.Вид = ВидПоляФормы.ПолеКартинки;
	Картинка.ПутьКДанным = СтрШаблон("Объект.ПрикрепленныеФото[%1].АдресКартинки", Формат(Индекс, "ЧН=0; ЧГ="));
	Картинка.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	Картинка.РазмерКартинки = РазмерКартинки.Пропорционально;
	
	КнопкаУдалить = Элементы.Добавить(ИмяСтраницы + "Удалить", Тип("КнопкаФормы"), Картинка.КонтекстноеМеню);
	КнопкаУдалить.ИмяКоманды = "УдалитьТекущееФото";
	
	Возврат Страница;
КонецФункции

#КонецОбласти
