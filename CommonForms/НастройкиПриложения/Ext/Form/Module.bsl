
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ПоказатьСписокРезервныхКопий(Команда)
	СписокКопий = СписокРезервныхКопий();
	Если СписокКопий.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru='Не обнаружено ни одной резервной копии'"));
		Возврат;
	КонецЕсли;
	
	ВыбранныйЭлемент = Ждать СписокКопий.ВыбратьЭлементАсинх(НСтр("ru='Выберите резервную копию'"));
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ИмяВыбранногоФайла = ВыбранныйЭлемент.Значение;
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить("Восстановить", НСтр("ru='Восстановить'"));
	Кнопки.Добавить("Удалить", НСтр("ru='Удалить'"));
	Кнопки.Добавить(КодВозвратаДиалога.Отмена);
	ТекстВопроса = СтрШаблон(НСтр("ru = 'Выбрана %1
                                   |Выберите действие или нажмите ""Отмена""'"), ВыбранныйЭлемент.Представление);
	Действие = Ждать ВопросАсинх(ТекстВопроса, Кнопки);
	
	Если Действие = "Восстановить" Тогда
		
		Ответ = Ждать ВопросАсинх(НСтр("ru = 'Данные приложения будут заменены данными из резервной копии.
                                        |Продолжить?'"), РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
		
		ВосстановитьРезервнуюКопиюНаСервере(ИмяВыбранногоФайла);
		ПоказатьПредупреждение(, НСтр("ru='Данные успешно восстановлены'"));
		Прочитать();
		
	ИначеЕсли Действие = "Удалить" Тогда
		
		Ответ = Ждать ВопросАсинх(НСтр("ru = 'Резервная копия будет безвозвратно удалена с Яндекс.Диска.
                                        |Продолжить?'"), РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
		
		УдалитьРезервнуюКопиюНаСервере(ИмяВыбранногоФайла);
		ПоказатьПредупреждение(, НСтр("ru='Копия успешно удалена'"));
		
	Иначе
		ВызватьИсключение НСтр("ru='Некорректное действие'");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРезервнуюКопию(Команда)
	СоздатьРезервнуюКопиюНаСервере();
	ПоказатьПредупреждение(, НСтр("ru='Резервная копия успешно создана'"));
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодключениеКЯндексДиску(Команда)
	Попытка
		ПроверитьПодключениеКЯндексДискуНаСервере();
		ПоказатьПредупреждение(, НСтр("ru='Подключение успешно'"));
	Исключение
		Шаблон = НСтр("ru='Не удалось связаться с Яндекс.Диском: %1'");
		ПоказатьПредупреждение(, СтрШаблон(Шаблон, ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
	КонецПопытки;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ПроверитьПодключениеКЯндексДискуНаСервере()
	РезервноеКопирование.ПроверитьПодключениеКWebDav();
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокРезервныхКопий()
	ТаблицаКопий = РезервноеКопирование.ДоступныеРезервныеКопии();
	ТаблицаКопий.Сортировать("Дата Убыв");
	
	КопииСписком = Новый СписокЗначений;
	Для Каждого СтрКопии Из ТаблицаКопий Цикл
		ШаблонПредставления = НСтр("ru='Копия от %1'");
		Представление = СтрШаблон(ШаблонПредставления, Формат(СтрКопии.Дата, "ДФ='dd.MM.yyyy HH:mm'"));
		КопииСписком.Добавить(СтрКопии.ИмяФайла, Представление);
	КонецЦикла;
	
	Возврат КопииСписком;
КонецФункции

&НаСервереБезКонтекста
Процедура СоздатьРезервнуюКопиюНаСервере()
	РезервноеКопирование.СоздатьРезервнуюКопию();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВосстановитьРезервнуюКопиюНаСервере(Знач ИмяФайла)
	РезервноеКопирование.ВосстановитьРезервнуюКопию(ИмяФайла);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьРезервнуюКопиюНаСервере(Знач ИмяФайла)
	РезервноеКопирование.УдалитьРезервнуюКопию(ИмяФайла);
КонецПроцедуры

#КонецОбласти
